apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.android["compileSdkVersion"]
    buildToolsVersion rootProject.ext.android["buildToolsVersion"]
    defaultConfig {
        applicationId "liyiwei.baseapk"
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]
        //配置阿里ARouter路由
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
        // 64k
        multiDexEnabled true
        // 支持模糊视图
        renderscriptSupportModeEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    //jdk1.8
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    //避免项目中存在相同的jar包，导致运行失败
    dexOptions {
        preDexLibraries = false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    if (!rootProject.ext.isCustomLibraryApplication) {
        implementation project(':customlibrary')//自定义模块(内含第三方库模块)
    }
    if (!rootProject.ext.isBaseMVPmodeApplication) {
        implementation project(':basemvpmode')//mvp模块
    }
    if (!rootProject.ext.isBaseNetWorkApplication) {
        implementation project(':basenetwork')//网络模块
    }
    if (!rootProject.ext.isBaseRouteApplication) {
        implementation project(':baseroute')//路由模块
    }
    if (!rootProject.ext.isLiveDataApplication) {
        implementation project(':livedatabus')//livedatabus模块
    }
//    if (!rootProject.ext.isBaseAppmodeApplication) {
//        implementation project(':appcrash')//范例模块
//    }

    api project(':baseresource')
    api project(':baselibrary')

    testImplementation rootProject.ext.dependencies["junit"]
    androidTestImplementation rootProject.ext.dependencies["runnerX"]
    androidTestImplementation rootProject.ext.dependencies["espresso-coreX"]
}
